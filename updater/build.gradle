/*
 * SPDX-FileCopyrightText: 2023 The Calyx Institute
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'org.jlleitschuh.gradle.ktlint'
}

android {
    namespace 'org.calyxos.lupin.updater'
    compileSdk sdk

    defaultConfig {
        applicationId "org.calyxos.lupin.updater"
        minSdk 33
        targetSdk sdk
        versionCode 1
        versionName "0.1"
    }

    signingConfigs {
        aosp {
            // Generated from the AOSP testkey:
            // https://android.googlesource.com/platform/build/+/refs/tags/android-11.0.0_r29/target/product/security/testkey.pk8
            keyAlias "testkey"
            keyPassword "testkey"
            storeFile file("../shared/testkey.jks")
            storePassword "testkey"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.aosp
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.aosp
        }
    }
    buildFeatures {
        buildConfig true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$compose_compiler_version"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lint {
        lintConfig file("../shared/lint.xml")
    }
    testOptions {
        unitTests {
            all {
                useJUnitPlatform()
                testLogging {
                    events "skipped", "failed"
                }
            }
            returnDefaultValues = true
        }
    }
}

dependencies {
    implementation project(':shared')

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    kapt 'androidx.hilt:hilt-compiler:1.1.0'

    implementation 'androidx.hilt:hilt-work:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    implementation 'androidx.compose.runtime:runtime-livedata:1.5.4'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.1'
    testImplementation 'io.mockk:mockk:1.13.8'
    testImplementation 'ch.qos.logback:logback-classic:1.4.11'
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}

apply from: "${rootProject.rootDir}/gradle/ktlint.gradle"
